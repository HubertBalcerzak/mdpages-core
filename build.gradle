plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id "org.springframework.boot" version "2.2.4.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
//    kotlin "jvm" version "1.3.61"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.3.61"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.61"

    id "org.asciidoctor.convert" version "1.5.9.2"
    id 'org.jetbrains.dokka' version '0.9.18'

}

group 'pl.starchasers'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    jsondoclet
}

dependencies {
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.5.4'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation 'io.jsonwebtoken:jjwt:0.9.0'
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:2.2.4.RELEASE"

    implementation "org.springframework.boot:spring-boot-starter-security:2.2.4.RELEASE"
    implementation "org.springframework.boot:spring-boot-starter-web:2.2.4.RELEASE"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.github.ben-manes.caffeine:caffeine:2.8.1"


    testImplementation ("org.springframework.boot:spring-boot-starter-test:2.2.4.RELEASE")  {
        exclude group: "org.junit.vintage:junit-vintage-engine"
    }
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation group: 'no.skatteetaten.aurora', name: 'mockmvc-extensions-kotlin', version: '1.0.4'
    testImplementation "com.h2database:h2:1.4.200"

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation group: 'capital.scalable', name: 'spring-auto-restdocs-core', version: '2.0.7'
    jsondoclet group: 'capital.scalable', name: 'spring-auto-restdocs-json-doclet', version: '2.0.7'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

ext {
    snippetsDir = file('build/generated-snippets')
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

test {
    useJUnitPlatform()
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir

    dependsOn dokka
}

asciidoctor {
    inputs.dir snippetsDir
    outputDir = file("$buildDir/generated-docs")
    options backend: "html", doctype: "book"
    attributes "source-highlighter": "highlightjs", "snippets": snippetsDir

    dependsOn test
}


dokka {
    outputFormat = "auto-restdocs-json"
    outputDirectory = javadocJsonDir
    includeNonPublic = true
    dokkaFatJar = "capital.scalable:spring-auto-restdocs-dokka-json:2.0.7"
}